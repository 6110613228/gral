description = 'GRAL core'

dependencies {
	testCompile(group: 'junit', name: 'junit', version:'4.10')
}

buildscript {
	apply from: 'https://github.com/valkolovos/gradle_cobertura/raw/master/repo/gradle_cobertura/gradle_cobertura/1.0/coberturainit.gradle'
}

task report(dependsOn: "cobertura") {
	description = "Generates reports."
}

apply plugin: "distribution"
distributions {
	main {
		contents {
			from(rootDir) {
				include("CHANGES")
				include("README")
			}
			from(projectDir) {
				include("src/**")
			}
			into("doc") {
				into("javadoc") {
					from javadoc
				}
				into("rst") {
					from("${projectDir}/src/rst")
				}
			}
			into("lib") {
				from jar
				from configurations.runtime
			}
		}
	}
}

task shrink(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
	description = "Uses ProGuard to reduce the code size of this project."
	// Configure ProGuard
	configuration("${projectDir}/src/etc/proguard.conf")
	target(targetCompatibility.toString())
	injars(jar.archivePath)
	outjars("${libsDir}/shrunk/${jar.archiveName}")
	libraryjars(runtimeJar)
	libraryjars(configurations.runtime)

	doLast {
		// Use the shrunk jar for all further processing
		jar.destinationDir = new File("${libsDir}/shrunk/")
	}
}

apply plugin: 'maven'
uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "http://mvn.erichseifert.de/maven2") {
				authentication(userName: "mavenUser", password: "mavenPassword")
			}
			pom.project {
				inceptionYear(inceptionYear)
				licenses {
					license {
						name("GNU Library or Lesser General Public License (LGPL)")
						url("http://www.gnu.org/licenses/lgpl.txt")
						distribution("main")
					}
				}
				issueManagement {
					system("trac")
					url("http://trac.erichseifert.de/gral/")
				}
				scm {
					connection("scm:git:git://erichseifert.de/gral.git")
					developerConnection("scm:git:ssh://git@erichseifert.de/gral.git")
					tag("HEAD")
				}
			}
		}
	}
}

// Include the proguard-gradle plugin
buildscript {
	repositories {
		mavenLocal()
		maven { url "http://mvn.erichseifert.de/maven2" }
		mavenCentral()
	}
	dependencies {
		classpath(group: "net.sf.proguard", name: "proguard-gradle", version: "4.+")
	}
}